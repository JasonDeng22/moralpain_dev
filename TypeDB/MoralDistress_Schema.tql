define

## "People" concepts / entities

person sub entity,
    abstract,
    owns first-name,
    owns last-name,
    owns gender,
    owns age;

patient sub person,
    owns illness,
    owns current-condition,
    plays nurse-patient-relation:patient;

user sub person,
    owns email @key,
    owns employeeID,
    owns userID,
    plays nurse-patient-relation:nurse,
    plays auth-relation:user,
    plays hospital-employment:employee;
    # owns password;

team-member sub person,
    owns email @key,
    plays study-team:member,
    plays approval-team:approver;

## "People" attributes

first-name sub attribute, value string;
last-name sub attribute, value string;
email sub attribute, value string;
gender sub attribute, value string, regex "^(male|female|other)$";
illness sub attribute, value string;
current-condition sub attribute, value string;
employeeID sub attribute, value string;
age sub attribute, value long;
duration sub attribute, value long;

## "People" relations

nurse-patient-relation sub relation, 
    owns duration, # duration in days (i.e. 16 days)
    relates patient,
    relates nurse;

## Study Entities

study sub entity,
    owns study-name,
    owns protocol,
    plays study-team:study,
    plays approval-team:study;

## Study Relations

study-team sub relation,
    owns team-name,
    relates member,
    relates study;

approval-team sub relation,
    owns team-name,
    relates approver,
    relates study;

## Study Attributes

protocol sub attribute, value string;
study-name sub attribute, value string;
team-name sub attribute, value string;

## Moral Distress Entities

moral-distress-report sub entity,
    owns date,
    owns moral-distress-score,
    plays report-has-relation:report,
    plays user-report-relation:report;

temperature-reading sub entity,
    owns temperature,
    plays report-has-relation:property;

hospital sub entity,
    owns hospital-name,
    owns hospital-location,
    owns hospital-specialization,
    owns hospital-website,
    owns hospital-phone-number,
    plays hospital-employment:employer;

contributing-factor sub entity,
    owns patient-name,
    owns reaction,
    owns content,
    owns delivery-method,
    plays report-has-relation:property;

## Moral Distress Attributes

date sub attribute, value datetime;
moral-distress-score sub attribute, value long;
patient-name sub attribute, value string;
reaction sub attribute, value string;
news sub attribute, abstract, value string;
content sub news;
temperature sub attribute, value long;
delivery-method sub news;
department sub attribute, value string;
hospital-name sub attribute, value string;
hospital-location sub attribute, value string;
hospital-specialization sub attribute, value string;
hospital-website sub attribute, value string;
hospital-phone-number sub attribute, value string;

## Moral Distress Relationships

user-report-relation sub relation,
    relates user,
    relates report;

hospital-employment sub relation,
    owns department,
    relates employee,
    relates employer;

report-has-relation sub relation,
    relates property,
    relates report;

## Authentication Concepts

permissions sub entity,
    owns permission-name,
    owns permission-id,
    owns permission-description,
    plays auth-relation:permissions;

login sub entity,
    owns password,
    owns auth-token,
    owns login-userID,
    owns date,
    plays auth-relation:login;

## Authentication Attributes

password sub attribute, value string;
userID sub attribute, value string;
auth-token sub attribute, value string;
permission-name sub attribute, value string;
permission-id sub attribute, value string;
permission-description sub attribute, value string;
login-userID sub attribute, value string;

## Authetication Relations

auth-relation sub relation,
    relates user,
    relates login, 
    relates permissions;

